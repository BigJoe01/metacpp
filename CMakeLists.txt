CMAKE_MINIMUM_REQUIRED(VERSION 2.8)
PROJECT(metacpp)
SET(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_SOURCE_DIR}/cmake/Modules/")
INCLUDE("${CMAKE_SOURCE_DIR}/cmake/Macros/ScanSources.cmake")
SET(VERSION_FILE "${CMAKE_SOURCE_DIR}/version")
SET(README_FILE "${CMAKE_SOURCE_DIR}/README.md")

SET(SRC_DIRS
    "${CMAKE_SOURCE_DIR}/src"
    "${CMAKE_SOURCE_DIR}/src/utils"
    "${CMAKE_SOURCE_DIR}/src/core"
    "${CMAKE_SOURCE_DIR}/src/db"
    "${CMAKE_SOURCE_DIR}/src/db/sql"
    "${CMAKE_SOURCE_DIR}/src/db/sql/connectors"
    "${CMAKE_SOURCE_DIR}/src/db/nosql"
    "${CMAKE_SOURCE_DIR}/src/db/nosql/connectors"
)

FIND_PACKAGE(JsonCpp)
IF(JSONCPP_FOUND)
    LIST(APPEND LIBS ${JSONCPP_LIBRARIES})
    LIST(APPEND INCLUDE_DIRS ${JSONCPP_INCLUDE_DIR})
    LIST(APPEND SRC_DIRS "${CMAKE_SOURCE_DIR}/src/serialization/json")
    ADD_DEFINITIONS(-DHAVE_JSONCPP)
ENDIF(JSONCPP_FOUND)

FIND_PACKAGE(Sqlite3)
IF(SQLITE3_FOUND)
    LIST(APPEND LIBS ${SQLITE3_LIBRARIES})
    LIST(APPEND INCLUDE_DIRS ${SQLITE3_INCLUDE_DIR})
    LIST(APPEND SRC_DIRS "${CMAKE_SOURCE_DIR}/src/db/sql/connectors/sqlite3")
    ADD_DEFINITIONS(-DHAVE_SQLITE3)
ENDIF(SQLITE3_FOUND)

FIND_PACKAGE(PostgreSQL)
IF(PostgreSQL_FOUND)
    LIST(APPEND LIBS ${PostgreSQL_LIBRARIES})
    LIST(APPEND INCLUDE_DIRS ${PostgreSQL_INCLUDE_DIRS})
    LIST(APPEND SRC_DIRS "${CMAKE_SOURCE_DIR}/src/db/sql/connectors/postgres")
    ADD_DEFINITIONS(-DHAVE_POSTGRES)
ENDIF(PostgreSQL_FOUND)

FIND_PACKAGE(MongoDB)
IF(MongoDB_FOUND)
    LIST(APPEND LIBS ${MongoDB_LIBRARIES})
    LIST(APPEND INCLUDE_DIRS ${MongoDB_INCLUDE_DIR})
    LIST(APPEND SRC_DIRS "${CMAKE_SOURCE_DIR}/src/serialization/bson")
    LIST(APPEND SRC_DIRS "${CMAKE_SOURCE_DIR}/src/db/nosql/connectors/mongodb")
    ADD_DEFINITIONS(-DHAVE_MONGODB)
ENDIF(MongoDB_FOUND)

IF(UNIX)
    FIND_PACKAGE(Iconv REQUIRED)
    LIST(APPEND LIBS ${ICONV_LIBRARIES})
    LIST(APPEND INCLUDE_DIRS ${ICONV_INCLUDE_DIR})
ENDIF(UNIX)

# read version information
FILE(STRINGS ${VERSION_FILE} PRO_VERSION)

LIST(APPEND INCLUDE_DIRS ${SRC_DIRS})
STRING(REPLACE ";" "\" \"" SOURCES_STR "${SRC_DIRS}")

IF("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Clang")
    # using LLVM Clang
    LIST(APPEND CMAKE_CXX_FLAGS "-Wall -Wextra -Wno-unused-private-field -std=c++11")
ELSEIF("${CMAKE_CXX_COMPILER_ID}" STREQUAL "GNU")
    # using GNUCXX
    LIST(APPEND CMAKE_CXX_FLAGS "-Wall -Wextra -Wno-invalid-offsetof -std=c++11")
ELSEIF("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Intel")
    # using Intel C++
ELSEIF("${CMAKE_CXX_COMPILER_ID}" STREQUAL "MSVC")
    # using Visual Studio C++
ENDIF()

ADD_SOURCE_DIRECTORY(SOURCES HEADERS "${SRC_DIRS}")

INCLUDE_DIRECTORIES(${INCLUDE_DIRS})
ADD_DEFINITIONS(-DVERSION=${PRO_VERSION})

ADD_LIBRARY(${CMAKE_PROJECT_NAME} ${SOURCES} ${HEADERS} ${VERSION_FILE} ${README_FILE})

TARGET_LINK_LIBRARIES(${CMAKE_PROJECT_NAME} ${LIBS})

FIND_PACKAGE(GTest)
# configure tests
IF(GTEST_FOUND)
    ENABLE_TESTING()
    SET(TEST_SRC_DIRS "test")

    ADD_SOURCE_DIRECTORY(TEST_SOURCES TEST_HEADERS "${TEST_SRC_DIRS}")

    ADD_EXECUTABLE(${CMAKE_PROJECT_NAME}-test ${TEST_SOURCES} ${TEST_HEADERS})
    TARGET_LINK_LIBRARIES(${CMAKE_PROJECT_NAME}-test ${CMAKE_PROJECT_NAME} ${GTEST_LIBRARIES})

    ADD_TEST("StringTest" "${CMAKE_BINARY_DIR}/${CMAKE_PROJECT_NAME}-test" "--gtest_filter=StringTest*")
    ADD_TEST("ObjectTest" "${CMAKE_BINARY_DIR}/${CMAKE_PROJECT_NAME}-test" "--gtest_filter=ObjectTest*")
    ADD_TEST("SqlTest" "${CMAKE_BINARY_DIR}/${CMAKE_PROJECT_NAME}-test" "--gtest_filter=SqlTest*")
    ADD_TEST("VariantTest" "${CMAKE_BINARY_DIR}/${CMAKE_PROJECT_NAME}-test" "--gtest_filter=*VariantTest*")
    ADD_TEST("DateTimeTest" "${CMAKE_BINARY_DIR}/${CMAKE_PROJECT_NAME}-test" "--gtest_filter=DateTimeTest*")
ENDIF(GTEST_FOUND)

FIND_PACKAGE(Doxygen)
IF(DOXYGEN_FOUND)
    CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/Doxyfile.in ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile @ONLY)
    ADD_CUSTOM_TARGET(doc
        ${DOXYGEN_EXECUTABLE} ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile
        WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
        COMMENT "Generating API documentation with Doxygen" VERBATIM
    )
ENDIF(DOXYGEN_FOUND)

INSTALL(FILES ${HEADERS} DESTINATION "include/metacpp")
INSTALL(TARGETS ${CMAKE_PROJECT_NAME} LIBRARY DESTINATION "lib" ARCHIVE DESTINATION "lib")
