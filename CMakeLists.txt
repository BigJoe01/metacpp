CMAKE_MINIMUM_REQUIRED(VERSION 2.8)
PROJECT(metacpp)
LIST(APPEND CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake/Modules/")
LIST(APPEND CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/coveralls-cmake/cmake/")
INCLUDE("${CMAKE_SOURCE_DIR}/cmake/Macros/ScanSources.cmake")
SET(VERSION_FILE "${CMAKE_SOURCE_DIR}/version")
SET(README_FILE "${CMAKE_SOURCE_DIR}/README.md")

SET(SRC_DIRS
    "${CMAKE_SOURCE_DIR}/src"
    "${CMAKE_SOURCE_DIR}/src/utils"
    "${CMAKE_SOURCE_DIR}/src/core"
    "${CMAKE_SOURCE_DIR}/src/db"
    "${CMAKE_SOURCE_DIR}/src/serialization"
    "${CMAKE_SOURCE_DIR}/src/db/sql"
    "${CMAKE_SOURCE_DIR}/src/db/sql/connectors"
    "${CMAKE_SOURCE_DIR}/src/scripting"
)

IF (MSVC)
    LIST(APPEND SRC_DIRS "${CMAKE_SOURCE_DIR}/src/compat")

    EXECUTE_PROCESS(COMMAND conan.exe install "${CMAKE_SOURCE_DIR}")
    INCLUDE("${CMAKE_SOURCE_DIR}/conanbuildinfo.cmake")
    CONAN_BASIC_SETUP()
ENDIF(MSVC)

FIND_PACKAGE(JsonCpp)
IF(JSONCPP_FOUND)
    ADD_SOURCE_DIRECTORY(JSON_SOURCES JSON_HEADERS "${CMAKE_SOURCE_DIR}/src/serialization/json")
    LIST(APPEND INCLUDE_DIRS ${JSONCPP_INCLUDE_DIR} "${CMAKE_SOURCE_DIR}/src/serialization/json")
    SET(JSON_TARGET_NAME "${CMAKE_PROJECT_NAME}-json")
    ADD_LIBRARY(${JSON_TARGET_NAME} ${JSON_SOURCES} ${JSON_HEADERS})
    TARGET_LINK_LIBRARIES(${JSON_TARGET_NAME} ${CMAKE_PROJECT_NAME} ${LIBS} ${JSONCPP_LIBRARIES})
    SET(HAVE_JSONCPP 1)
ENDIF(JSONCPP_FOUND)

FIND_PACKAGE(Sqlite3)
IF(SQLITE3_FOUND)
    ADD_SOURCE_DIRECTORY(SQLITE3_CONNECTOR_SOURCES SQLITE3_CONNECTOR_HEADERS "${CMAKE_SOURCE_DIR}/src/db/sql/connectors/sqlite3")
    LIST(APPEND INCLUDE_DIRS ${SQLITE3_INCLUDE_DIR} "${CMAKE_SOURCE_DIR}/src/db/sql/connectors/sqlite3")
    SET(SQLITE3_CONNECTOR_TARGET_NAME "${CMAKE_PROJECT_NAME}-connector-sqlite3")
    ADD_LIBRARY(${SQLITE3_CONNECTOR_TARGET_NAME} ${SQLITE3_CONNECTOR_SOURCES} ${SQLITE3_CONNECTOR_HEADERS})
    TARGET_LINK_LIBRARIES(${SQLITE3_CONNECTOR_TARGET_NAME} ${CMAKE_PROJECT_NAME} ${LIBS} ${SQLITE3_LIBRARIES})
    SET(HAVE_SQLITE3 1)
ENDIF(SQLITE3_FOUND)

FIND_PACKAGE(PostgreSQL)
IF(PostgreSQL_FOUND)
    ADD_SOURCE_DIRECTORY(POSTGRES_CONNECTOR_SOURCES POSTGRES_CONNECTOR_HEADERS "${CMAKE_SOURCE_DIR}/src/db/sql/connectors/postgres")
    LIST(APPEND INCLUDE_DIRS ${PostgreSQL_INCLUDE_DIRS} "${CMAKE_SOURCE_DIR}/src/db/sql/connectors/postgres")
    SET(POSTGRES_CONNECTOR_TARGET_NAME "${CMAKE_PROJECT_NAME}-connector-postgres")
    ADD_LIBRARY(${POSTGRES_CONNECTOR_TARGET_NAME} ${POSTGRES_CONNECTOR_SOURCES} ${POSTGRES_CONNECTOR_HEADERS})
    TARGET_LINK_LIBRARIES(${POSTGRES_CONNECTOR_TARGET_NAME} ${CMAKE_PROJECT_NAME} ${LIBS} ${PostgreSQL_LIBRARIES})
    SET(HAVE_POSTGRES 1)
ENDIF(PostgreSQL_FOUND)

FIND_PACKAGE(MySQL)
IF (MYSQL_FOUND)
    ADD_SOURCE_DIRECTORY(MYSQL_CONNECTOR_SOURCES MYSQL_CONNECTOR_HEADERS "${CMAKE_SOURCE_DIR}/src/db/sql/connectors/mysql")
    LIST(APPEND INCLUDE_DIRS ${MYSQL_INCLUDE_DIR} "${CMAKE_SOURCE_DIR}/src/db/sql/connectors/mysql")
    SET(MYSQL_CONNECTOR_TARGET_NAME "${CMAKE_PROJECT_NAME}-connector-mysql")
    ADD_LIBRARY(${MYSQL_CONNECTOR_TARGET_NAME} ${MYSQL_CONNECTOR_SOURCES} ${MYSQL_CONNECTOR_HEADERS})
    TARGET_LINK_LIBRARIES(${MYSQL_CONNECTOR_TARGET_NAME} ${CMAKE_PROJECT_NAME} ${LIBS} ${MYSQL_CLIENT_LIBS})
    SET(HAVE_MYSQL 1)
ENDIF (MYSQL_FOUND)

FIND_PACKAGE(SpiderMonkey)
IF(SPIDERMONKEY_FOUND)
    ADD_SOURCE_DIRECTORY(JS_ENGINE_SOURCES JS_ENGINE_HEADERS "${CMAKE_SOURCE_DIR}/src/scripting/js")
    LIST(APPEND INCLUDE_DIRS ${SPIDERMONKEY_INCLUDE_DIR} "${CMAKE_SOURCE_DIR}/src/scripting/js")
    SET(JS_ENGINE_TARGET_NAME "${CMAKE_PROJECT_NAME}-scripting-js")
    ADD_LIBRARY(${JS_ENGINE_TARGET_NAME} ${JS_ENGINE_SOURCES} ${JS_ENGINE_HEADERS})
    TARGET_LINK_LIBRARIES(${JS_ENGINE_TARGET_NAME} ${CMAKE_PROJECT_NAME} ${LIBS} ${SPIDERMONKEY_LIBRARIES})
    SET(HAVE_SPIDERMONKEY 1)
ENDIF(SPIDERMONKEY_FOUND)

IF(UNIX)
    FIND_PACKAGE(Iconv REQUIRED)
    LIST(APPEND LIBS ${ICONV_LIBRARIES})
    LIST(APPEND INCLUDE_DIRS ${ICONV_INCLUDE_DIR})
ENDIF(UNIX)

# read version information
FILE(STRINGS ${VERSION_FILE} METACPP_VERSION_STRING)
STRING(REPLACE "." ";" PRO_VERSION_LIST ${METACPP_VERSION_STRING})
LIST(GET PRO_VERSION_LIST 0 METACPP_VERSION_MAJOR)
LIST(GET PRO_VERSION_LIST 1 METACPP_VERSION_MINOR)

LIST(APPEND INCLUDE_DIRS ${SRC_DIRS})
STRING(REPLACE ";" "\" \"" SOURCES_STR "${SRC_DIRS}")

IF(${CMAKE_CXX_COMPILER_ID} STREQUAL "Clang")
    # using LLVM Clang
    SET(CMAKE_CXX_FLAGS  "${CMAKE_CXX_FLAGS} -Wall -Wextra -Wno-unused-private-field -std=c++11")
    LIST(APPEND LIBS "-lpthread")
ELSEIF(${CMAKE_CXX_COMPILER_ID} STREQUAL "GNU")
    # using GNUCXX
    SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall -Wextra -Wno-invalid-offsetof -Wno-unused-parameter -Wno-ignored-qualifiers -std=c++11")
    LIST(APPEND LIBS "-lpthread" "-ldl -lz")
ENDIF()

ADD_SOURCE_DIRECTORY(SOURCES HEADERS "${SRC_DIRS}")

IF(COVERALLS)
    MESSAGE(STATUS "Building with coverage analysis")
    INCLUDE(Coveralls)
    COVERALLS_TURN_ON_COVERAGE()
    SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fno-inline -fno-inline-small-functions -fno-default-inline -fno-elide-constructors")
    COVERALLS_SETUP("${SOURCES}" ${COVERALLS_UPLOAD})
ENDIF(COVERALLS)

CONFIGURE_FILE("${CMAKE_CURRENT_SOURCE_DIR}/config.h.in" "${CMAKE_CURRENT_BINARY_DIR}/config.h" @ONLY)
LIST(APPEND INCLUDE_DIRS ${CMAKE_CURRENT_BINARY_DIR})
LIST(APPEND HEADERS "${CMAKE_CURRENT_BINARY_DIR}/config.h")

INCLUDE_DIRECTORIES(${INCLUDE_DIRS})

IF(TEST_POSTGRES_DBNAME AND TEST_POSTGRES_DBUSER)
    MESSAGE(STATUS "Test postgres database: ${TEST_POSTGRES_DBNAME}")
    MESSAGE(STATUS "Test postgres user: ${TEST_POSTGRES_DBUSER}")
    ADD_DEFINITIONS("-DTEST_POSTGRES_DBNAME=${TEST_POSTGRES_DBNAME}")
    ADD_DEFINITIONS("-DTEST_POSTGRES_DBUSER=${TEST_POSTGRES_DBUSER}")
ENDIF(TEST_POSTGRES_DBNAME AND TEST_POSTGRES_DBUSER)

IF(TEST_MYSQL_URI)
    ADD_DEFINITIONS("-DTEST_MYSQL_URI=${TEST_MYSQL_URI}")
ENDIF(TEST_MYSQL_URI)

ADD_LIBRARY(${CMAKE_PROJECT_NAME} ${SOURCES} ${HEADERS} ${VERSION_FILE} ${README_FILE})
LIST(APPEND INSTALL_HEADERS ${HEADERS})
LIST(APPEND INSTALL_TARGETS ${CMAKE_PROJECT_NAME})

TARGET_LINK_LIBRARIES(${CMAKE_PROJECT_NAME} ${LIBS})

IF(JSONCPP_FOUND)
    LIST(APPEND INSTALL_HEADERS ${JSON_HEADERS})
    LIST(APPEND INSTALL_TARGETS ${JSON_TARGET_NAME})
ENDIF(JSONCPP_FOUND)

IF(SQLITE3_FOUND)
    LIST(APPEND INSTALL_HEADERS ${SQLITE3_CONNECTOR_HEADERS})
    LIST(APPEND INSTALL_TARGETS ${SQLITE3_CONNECTOR_TARGET_NAME})
ENDIF(SQLITE3_FOUND)

IF(PostgreSQL_FOUND)
    LIST(APPEND INSTALL_HEADERS "${POSTGRES_CONNECTOR_HEADERS}")
    LIST(APPEND INSTALL_TARGETS "${POSTGRES_CONNECTOR_TARGET_NAME}")
ENDIF(PostgreSQL_FOUND)

IF(MYSQL_FOUND)
    LIST(APPEND INSTALL_HEADERS ${MYSQL_CONNECTOR_HEADERS})
    LIST(APPEND INSTALL_TARGETS ${MYSQL_CONNECTOR_TARGET_NAME})
ENDIF(MYSQL_FOUND)

IF(SPIDERMONKEY_FOUND)
    LIST(APPEND INSTALL_HEADERS ${JS_ENGINE_HEADERS})
    LIST(APPEND INSTALL_TARGETS ${JS_ENGINE_TARGET_NAME})
ENDIF(SPIDERMONKEY_FOUND)

FIND_PACKAGE(GTest)
# configure tests
IF(GTEST_FOUND)
    ENABLE_TESTING()
    SET(TEST_SRC_DIRS "test")

    ADD_SOURCE_DIRECTORY(TEST_SOURCES TEST_HEADERS "${TEST_SRC_DIRS}")

    ADD_EXECUTABLE(${CMAKE_PROJECT_NAME}-test ${TEST_SOURCES} ${TEST_HEADERS})
    TARGET_LINK_LIBRARIES(${CMAKE_PROJECT_NAME}-test ${INSTALL_TARGETS} ${GTEST_LIBRARIES})

    ADD_TEST("StringTest" "${CMAKE_BINARY_DIR}/${CMAKE_PROJECT_NAME}-test" "--gtest_filter=StringTest*")
    ADD_TEST("ObjectTest" "${CMAKE_BINARY_DIR}/${CMAKE_PROJECT_NAME}-test" "--gtest_filter=ObjectTest*")
    ADD_TEST("VariantTest" "${CMAKE_BINARY_DIR}/${CMAKE_PROJECT_NAME}-test" "--gtest_filter=*VariantTest*")
    ADD_TEST("DateTimeTest" "${CMAKE_BINARY_DIR}/${CMAKE_PROJECT_NAME}-test" "--gtest_filter=DateTimeTest*")
    IF(SQLITE3_FOUND)
        ADD_TEST("SqliteTest" "${CMAKE_BINARY_DIR}/${CMAKE_PROJECT_NAME}-test" "--gtest_filter=SqliteTest*")
    ENDIF(SQLITE3_FOUND)
    IF(PostgreSQL_FOUND AND TEST_POSTGRES_DBNAME AND TEST_POSTGRES_DBUSER)
        ADD_TEST("PostgresTest" "${CMAKE_BINARY_DIR}/${CMAKE_PROJECT_NAME}-test" "--gtest_filter=PostgresTest*")
    ENDIF(PostgreSQL_FOUND AND TEST_POSTGRES_DBNAME AND TEST_POSTGRES_DBUSER)
    ADD_TEST("JSScriptTest" "${CMAKE_BINARY_DIR}/${CMAKE_PROJECT_NAME}-test" "--gtest_filter=JSScriptTest*")
ENDIF(GTEST_FOUND)

file(GLOB EXAMPLE_SOURCES "${CMAKE_SOURCE_DIR}/examples/*.cpp")

FOREACH(EXAMPLE_SOURCE ${EXAMPLE_SOURCES})
    GET_FILENAME_COMPONENT(EXAMPLE_NAME ${EXAMPLE_SOURCE} NAME_WE)
    ADD_EXECUTABLE(${EXAMPLE_NAME} EXCLUDE_FROM_ALL ${EXAMPLE_SOURCE})
    TARGET_LINK_LIBRARIES(${EXAMPLE_NAME} ${INSTALL_TARGETS})
    LIST(APPEND EXAMPLE_NAMES ${EXAMPLE_NAME})
ENDFOREACH(EXAMPLE_SOURCE)

ADD_CUSTOM_TARGET(examples)
ADD_DEPENDENCIES(examples ${EXAMPLE_NAMES})

FIND_PACKAGE(Doxygen)
IF(DOXYGEN_FOUND)
    CONFIGURE_FILE(${CMAKE_CURRENT_SOURCE_DIR}/Doxyfile.in ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile @ONLY)
    ADD_CUSTOM_TARGET(doc
        ${DOXYGEN_EXECUTABLE} ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile
        WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
        COMMENT "Generating API documentation with Doxygen" VERBATIM
    )
ENDIF(DOXYGEN_FOUND)

INSTALL(FILES ${INSTALL_HEADERS} DESTINATION "include/metacpp")
INSTALL(TARGETS ${INSTALL_TARGETS} LIBRARY DESTINATION "lib" ARCHIVE DESTINATION "lib")

IF(UNIX)
    SET(CPACK_GENERATOR "TGZ")
    SET(CPACK_PACKAGE_NAME ${CMAKE_PROJECT_NAME})
    SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY "C++ reflection library, ORM and JavaScript wrapper")
    SET(CPACK_PACKAGE_VENDOR "the-alien@live.ru")
    SET(CPACK_PACKAGE_CONTACT "the-alien@live.ru")
    SET(CPACK_PACKAGE_DESCRIPTION_FILE "${CMAKE_CURRENT_SOURCE_DIR}/README.md")
    SET(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE")
    SET(CPACK_PACKAGE_VERSION_MAJOR "${METACPP_VERSION_MAJOR}")
    SET(CPACK_PACKAGE_VERSION_MINOR "${METACPP_VERSION_MINOR}")
    SET(CPACK_PACKAGE_VERSION_PATCH "0")
    #SET(CPACK_INSTALL_PREFIX "/usr")
    #SET(CPACK_PACKAGING_INSTALL_PREFIX "/usr")
    #SET(CPACK_PACKAGE_DEFAULT_LOCATION "/usr")

    FIND_PROGRAM(DPKG_BINARY dpkg)
    IF(DPKG_BINARY)
        SET(CPACK_DEBIAN_PACKAGE_PROVIDES "${CPACK_PACKAGE_NAME}-dev")
        SET(CPACK_DEBIAN_PACKAGE_REPLACES "${CPACK_PACKAGE_NAME}-dev")
        #SET(CPACK_DEBIAN_PACKAGE_DEPENDS "libsqlite3,libmysqlclient,libpq,libmozjs-${SPIDERMONKEY_VERSION}")
        LIST(APPEND CPACK_GENERATOR "DEB")
    ENDIF(DPKG_BINARY)

    FIND_PROGRAM(RPMBUILD_BINARY rpmbuild)
    IF(RPMBUILD_BINARY)
        SET(CPACK_RPM_PACKAGE_RELOCATABLE TRUE)
        #SET(CPACK_RPM_PACKAGE_REQUIRES "sqlite-libs,mariadb-libs,postgresql-libs,mozjs${SPIDERMONKEY_VERSION}")
        LIST(APPEND CPACK_GENERATOR "RPM")
    ENDIF(RPMBUILD_BINARY)

    INCLUDE(CPack)
ENDIF(UNIX)
